plugins {
    id "org.springframework.boot" version "${springBootVersion}" apply false
    id "com.diffplug.spotless" version "${spotlessVersion}" apply false
    id "com.github.spotbugs" version "${spotbugsVersion}" apply false
    id "org.jreleaser" version "${jReleaserVersion}" apply false
    id "io.spring.nullability" version "+" apply false
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "io.spring.nullability"

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ["-parameters"]
        options.encoding = "UTF-8"
    }

    javadoc {
        // Suppress warnings about missing Javadoc comments
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    configurations {
        optional
        compileOnly.extendsFrom optional
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compileOnly("org.projectlombok:lombok:+")
        annotationProcessor("org.projectlombok:lombok:+")
        testCompileOnly("org.projectlombok:lombok:+")
        testAnnotationProcessor("org.projectlombok:lombok:+")
        compileOnly("com.github.spotbugs:spotbugs-annotations:${spotbugsAnnotationsVersion}")

        implementation("org.jspecify:jspecify:+")

        testImplementation(platform("org.junit:junit-bom:+"))
        testImplementation("org.junit.jupiter:junit-jupiter")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
        testImplementation("org.assertj:assertj-core:+")
    }

    test {
        useJUnitPlatform()
        jvmArgs("-XX:+EnableDynamicAgentLoading")
    }

    apply plugin: "com.diffplug.spotless"
    spotless {
        encoding "UTF-8"
        java {
            toggleOffOn()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            palantirJavaFormat()

            targetExclude "**/generated/**"

            custom("Refuse wildcard imports", {
                if (it =~ /\nimport .*\*;/) {
                    throw new IllegalStateException("Do not use wildcard imports, 'spotlessApply' cannot resolve this issue, please fix it manually.")
                }
            } as Closure<String>)
        }
    }

    apply plugin: "com.github.spotbugs"
    spotbugs {
        spotbugsTest.enabled = false
        omitVisitors.addAll "FindReturnRef", "DontReusePublicIdentifiers"
        excludeFilter = file("${rootDir}/gradle/config/spotbugs/exclude.xml")
    }
}

apply from: "${rootDir}/gradle/jreleaser.gradle"
